message(STATUS "CMAKE_CXX_COMPILER_ID : ${CMAKE_CXX_COMPILER_ID} CMAKE_CXX_COMPILER_FRONTEND_VARIANT : ${CMAKE_CXX_COMPILER_FRONTEND_VARIANT}")

list(FIND CMAKE_CXX_COMPILE_FEATURES cxx_std_11 CPP_STANDARD_FOUND)
if(CPP_STANDARD_FOUND STREQUAL "-1" OR ((CMAKE_CXX_COMPILER_ID STREQUAL "GNU") AND ${CMAKE_CXX_COMPILER_VERSION} VERSION_LESS 4.8))
  include(Missives)
  missive(ERROR "Compiler ${CMAKE_CXX_COMPILER_ID} is too old to run doctest")
  missive(ERROR "Tests disabled")
  return()
else()
  set(doctest_DOCTEST_NO_INSTALL TRUE)
  include(Doctest)
endif()

if(POLICY CMP0110)
  cmake_policy(SET CMP0110 NEW)
endif()

add_library(KhaosTestFlags INTERFACE)

target_compile_options(KhaosTestFlags INTERFACE
  $<$<CXX_COMPILER_ID:Clang>:-Wall -Weverything -pedantic>
  $<$<CXX_COMPILER_ID:GNU>:-Wall -Wextra -pedantic>
  $<$<CXX_COMPILER_ID:Intel>:-Wall -Wextra -pedantic>
  $<$<CXX_COMPILER_ID:IntelLLVM>:-Wall -Wextra -pedantic>
  $<$<CXX_COMPILER_ID:MSVC>:/W4>
  $<$<C_COMPILER_ID:Clang>:-Wall -Weverything -pedantic>
  $<$<C_COMPILER_ID:GNU>:-Wall -Wextra -pedantic>
  $<$<C_COMPILER_ID:IntelLLVM>:-Wall -Wextra -pedantic>
  $<$<C_COMPILER_ID:Intel>:-Wall -Wextra -pedantic>
  $<$<C_COMPILER_ID:MSVC>:/W4>)

add_executable(KhaosVersion.test KhaosVersion.test.cpp)
target_link_libraries(KhaosVersion.test PRIVATE Khaos::Khaos doctest_with_main KhaosTestFlags)
target_compile_definitions(KhaosVersion.test PRIVATE "-DMAJOR=${Khaos_VERSION_MAJOR}" "-DMINOR=${Khaos_VERSION_MINOR}" "-DPATCH=${Khaos_VERSION_PATCH}" "-DTWEAK=${Khaos_VERSION_TWEAK}")
doctest_discover_tests(KhaosVersion.test)

add_executable(Version.test Version.test.cpp)
target_link_libraries(Version.test PRIVATE Khaos::Khaos doctest_with_main KhaosTestFlags)
target_compile_definitions(Version.test PRIVATE "-DMAJOR=${Khaos_VERSION_MAJOR}" "-DMINOR=${Khaos_VERSION_MINOR}" "-DPATCH=${Khaos_VERSION_PATCH}" "-DTWEAK=${Khaos_VERSION_TWEAK}")
doctest_discover_tests(Version.test)

add_executable(CompareVersion.test CompareVersion.test.cpp)
target_link_libraries(CompareVersion.test PRIVATE Khaos::Khaos doctest_with_main KhaosTestFlags)
target_compile_definitions(CompareVersion.test PRIVATE "-DMAJOR=${Khaos_VERSION_MAJOR}" "-DMINOR=${Khaos_VERSION_MINOR}" "-DPATCH=${Khaos_VERSION_PATCH}" "-DTWEAK=${Khaos_VERSION_TWEAK}")
doctest_discover_tests(CompareVersion.test)

add_executable(CStandards.test CStandards.test.cpp)
target_link_libraries(CStandards.test PRIVATE Khaos::Khaos doctest_with_main KhaosTestFlags)
doctest_discover_tests(CStandards.test)

add_executable(CXXStandards.test CXXStandards.test.cpp)
target_link_libraries(CXXStandards.test PRIVATE Khaos::Khaos doctest_with_main KhaosTestFlags)
doctest_discover_tests(CXXStandards.test)

if(${CMAKE_CXX_COMPILER_ID} STREQUAL "Clang")
  if(${CMAKE_CXX_COMPILER_VERSION} VERSION_GREATER "4")
    set(CPP_STANDARDS "98;11;14;17")
  elseif(${CMAKE_CXX_COMPILER_VERSION} VERSION_GREATER "3.6")
    set(CPP_STANDARDS "98;11;14")
  else()
    set(CPP_STANDARDS "98;11")
  endif()
elseif(${CMAKE_CXX_COMPILER_ID} STREQUAL "GNU")
  if(${CMAKE_CXX_COMPILER_VERSION} VERSION_GREATER "7")
    set(CPP_STANDARDS "98;11;14;17")
  elseif(${CMAKE_CXX_COMPILER_VERSION} VERSION_GREATER_EQUAL "5")
    set(CPP_STANDARDS "98;11;14")
  else()
    set(CPP_STANDARDS "98;11")
  endif()
endif()
if (WIN32)
  missive(WARN "Extensions are always ON on Windows")
  set(CPP_EXTENSIONS "ON")
else ()
  set(CPP_EXTENSIONS "ON;OFF")
endif ()
foreach(CPP_STANDARD ${CPP_STANDARDS})
  list(FIND CMAKE_CXX_COMPILE_FEATURES cxx_std_${CPP_STANDARD} CPP_STANDARD_FOUND)
  if(NOT CPP_STANDARD_FOUND STREQUAL "-1")
    foreach(CPP_EXTENSION ${CPP_EXTENSIONS})
      add_executable(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test LanguageCXX.test.cpp)
      if("${CPP_EXTENSION}" STREQUAL ON)
        target_compile_definitions(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test PRIVATE "-DCPP_STANDARD=${CPP_STANDARD}" "-DCPP_EXTENSIONS=1")
      else()
        target_compile_definitions(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test PRIVATE "-DCPP_STANDARD=${CPP_STANDARD}" "-DCPP_EXTENSIONS=0")
      endif()
      set_target_properties(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test PROPERTIES CXX_STANDARD ${CPP_STANDARD})
      set_target_properties(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test PROPERTIES CXX_STANDARD_REQUIRED TRUE)
      set_target_properties(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test PROPERTIES CXX_EXTENSIONS ${CPP_EXTENSION})
      if("${CPP_STANDARD}" STREQUAL 98)
        target_link_libraries(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test PRIVATE Khaos::Khaos KhaosTestFlags)
        add_test(NAME "Test the Language CXX${CPP_STANDARD} extensions ${CPP_EXTENSION}" COMMAND LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test)
      else()
        target_link_libraries(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test PRIVATE Khaos::Khaos doctest_with_main KhaosTestFlags)
        doctest_discover_tests(LanguageCXX${CPP_STANDARD}Extensions${CPP_EXTENSION}.test)
      endif()
    endforeach()
  endif()
endforeach()

if ("${CMAKE_C_COMPILER_FRONTEND_VARIANT}" STREQUAL "MSVC")
  missive(WARN "Clang-cl doesn't have 90 99 11 standard")
  set(CC_STANDARDS "17")
elseif (MSVC)
  missive(WARN "MSVC doesn't have 99 standard")
  set(CC_STANDARDS "90;11;17")
else ()
  set(CC_STANDARDS "90;99;11;17")
endif ()

if (WIN32)
  set(CC_EXTENSIONS "ON")
else ()
  set(CC_EXTENSIONS "ON;OFF")
endif ()

foreach(CC_STANDARD ${CC_STANDARDS})
  list(FIND CMAKE_C_COMPILE_FEATURES c_std_${CC_STANDARD} C_STANDARD_FOUND)
  if(NOT C_STANDARD_FOUND STREQUAL "-1")
    foreach(CC_EXTENSION ${CC_EXTENSIONS})
      add_executable(LanguageC${CC_STANDARD}Extensions${CC_EXTENSION}.test LanguageC.test.c)
      if("${CC_EXTENSION}" STREQUAL ON)
        target_compile_definitions(LanguageC${CC_STANDARD}Extensions${CC_EXTENSION}.test PRIVATE "-DC_STANDARD=${CC_STANDARD}" "-DC_EXTENSIONS=1")
      else()
        target_compile_definitions(LanguageC${CC_STANDARD}Extensions${CC_EXTENSION}.test PRIVATE "-DC_STANDARD=${CC_STANDARD}" "-DC_EXTENSIONS=0")
      endif()
      set_target_properties(LanguageC${CC_STANDARD}Extensions${CC_EXTENSION}.test PROPERTIES C_STANDARD ${CC_STANDARD})
      set_target_properties(LanguageC${CC_STANDARD}Extensions${CC_EXTENSION}.test PROPERTIES C_STANDARD_REQUIRED TRUE)
      set_target_properties(LanguageC${CC_STANDARD}Extensions${CC_EXTENSION}.test PROPERTIES C_EXTENSIONS ${CC_EXTENSION})
      target_link_libraries(LanguageC${CC_STANDARD}Extensions${CC_EXTENSION}.test PRIVATE Khaos::Khaos KhaosTestFlags)
      add_test(NAME "Test the language C${CC_STANDARD} extensions ${CC_EXTENSION}" COMMAND LanguageC${CC_STANDARD}Extensions${CC_EXTENSION}.test)
    endforeach()
  endif()
endforeach()
